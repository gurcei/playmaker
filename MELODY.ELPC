#output playmaker

trap runstop_trapper

.declarations
'------------
#declare chunk_idx = 0
#declare chunk_cnt = 0
#define chunk_max = 20

#declare vidx, idx
#declare cursor_x = 0
#declare cursor_y = 0

#declare v$(5,chunk_max)

#declare key$
#declare valid$="cdefgab0123456789 #$.hiqrswotuxmpl"
#declare val$
#declare tmpo% = 12

#declare esc_flag% = 0

print "{x93}"

.main
'----
  key off
  gosub draw_editor_screen
  gosub user_input
  goto main

.draw_editor_screen
'------------------
  cursor 0,0:print "playmaker v0.01 - by  gurce isikyildiz"
  gosub draw_current_chunk
  return

.draw_current_chunk
'------------------
  print "{x93}"
  print "tempo:      {x9D}{x9D}{x9D}{x9D}{x9D}"; tmpo%
  print "chunk_idx:    {x9D}{x9D}{x9D}"; chunk_idx  
   
  for vidx=0 to 5
    if cursor_y <> vidx then begin
      cursor 0,5+vidx*2:print chr$(27);"q";"v";vidx;": ";v$(vidx,chunk_idx);
    bend:else begin
      cursor 0,5+vidx*2:print chr$(27);"q";"v";vidx;": ";
      for idx=1 to len(v$(vidx,chunk_idx))
        if cursor_x = idx-1 then begin
          print "{x12}";mid$(v$(vidx,chunk_idx),idx,1);"{x92}";
        bend:else begin
          print mid$(v$(vidx,chunk_idx),idx,1);
        bend
      next idx
      if len(v$(vidx,chunk_idx)) = 0 or cursor_x=len(v$(vidx,chunk_idx)) then print "{x12} {x92}";
    bend
  next vidx
  return

.check_valid_key
'---------------
  for idx=1 to len(valid$)
    val$=mid$(valid$,idx,1)
    if key$= val$ then begin
      if cursor_x < len(v$(cursor_y,chunk_idx)) then begin
        v$(cursor_y,chunk_idx) = left$(v$(cursor_y,chunk_idx),cursor_x) {x5F}
          + val$ + mid$(v$(cursor_y,chunk_idx),cursor_x+1)
        cursor_x=cursor_x+1
        return
      bend
      v$(cursor_y,chunk_idx)=v$(cursor_y,chunk_idx)+val$
      cursor_x=cursor_x+1
      return
    bend
  next idx
  return

.forward_word
'------------
  if cursor_x = len(v$(cursor_y,chunk_idx)) then return

  do while mid$(v$(cursor_y,chunk_idx),cursor_x+1,1) <> " "
    cursor_x=cursor_x+1
    if cursor_x = len(v$(cursor_y,chunk_idx)) then return
  loop
  do while mid$(v$(cursor_y,chunk_idx),cursor_x+1,1) = " "
    cursor_x=cursor_x+1
    if cursor_x = len(v$(cursor_y,chunk_idx)) then return
  loop

  return

.previous_word
'-------------
  if cursor_x = 0 then return
  if cursor_x = 1 then cursor_x = 0: return

  cursor_x = cursor_x - 1
  do while mid$(v$(cursor_y,chunk_idx),cursor_x+1,1) = " "
    cursor_x=cursor_x-1
    if cursor_x = 0 then return
  loop
  do while mid$(v$(cursor_y,chunk_idx),cursor_x+1,1) <> " "
    cursor_x=cursor_x-1
    if cursor_x = 0 then return
  loop
  cursor_x=cursor_x+1

  return

.load
'----
  input "load name: ";key$
  if key$="" then return

  dopen #2,(key$),r,u8
  for chunk_idx = 0 to chunk_max-1
    for vidx=0 to 5
      input #2,v$(vidx, chunk_idx)
    next vidx
  next chunk_idx
  input #2,tmpo%

  dclose #2

  chunk_idx = 0
  return

.save
'----
  input "save name: ";key$
  if key$="" then return
  
  delete (key$)
  dopen #2,(key$),w,u8
  for chunk_idx = 0 to chunk_max-1
    for vidx=0 to 5
      print #2,v$(vidx, chunk_idx)
    next vidx
  next chunk_idx
  print #2,tmpo%

  dclose #2

  chunk_idx = 0
  return

.save_as_text
'------------
  input "save (as text) name: ";key$
  if key$="" then return

  delete (key$)
    
  dopen #2,(key$),w,u8
  for chunk_idx = 0 to chunk_max-1
    for vidx=0 to 5
      print #2, "v";chr$(49+vidx);"$(";str$(chunk_idx);")=";
      print #2, chr$(34);v$(vidx, chunk_idx);chr$(34)
    next vidx  
    print #2, ""
  next chunk_idx
  dclose #2

  chunk_idx = 0
  return

.user_input
'----------
  getkey key$

  ' down arrow?
  if key$="{x11}" and cursor_y<5 then begin
    cursor_y = cursor_y + 1
    if cursor_x > len(v$(cursor_y,chunk_idx)) then cursor_x = {x5F}
      len(v$(cursor_y,chunk_idx))
  bend

  ' up arrow?
  if key$="{x91}" and cursor_y>0 then begin
    cursor_y = cursor_y - 1
    if cursor_x > len(v$(cursor_y,chunk_idx)) then {x5F}
      cursor_x = len(v$(cursor_y,chunk_idx))
  bend

  ' right arrow?
  if key$="{x1D}" and cursor_x < len(v$(cursor_y,chunk_idx)) then begin
    cursor_x=cursor_x+1
  bend

  ' left arrow?
  if key$="{x9D}" and cursor_x > 0 then cursor_x=cursor_x-1

  ' backspace (inst/del)?
  if key$="{x14}" and cursor_x > 0 then begin
    v$(cursor_y,chunk_idx)=left$(v$(cursor_y,chunk_idx),cursor_x-1) {x5F}
      + mid$(v$(cursor_y,chunk_idx),cursor_x+1)
    cursor_x=cursor_x-1
  bend

  if esc_flag% then begin
    esc_flag%=0
    if key$ = "j" then begin:cursor_x=0:bend
    if key$ = "k" then begin:cursor_x=len(v$(cursor_y,chunk_idx)):bend
    key$=""
  bend

  if key$=chr$(27) then esc_flag%=abs(esc_flag%-1)

  ' ctrl-w = forward one word
  if key$="{x17}" then gosub forward_word

  ' ctrl-u = back one word
  if key$="{x15}" then gosub previous_word

  ' < / > = previous/next chunk
  if key$="<" and chunk_idx>0 then begin
    chunk_idx=chunk_idx-1
    cursor_x=0
  bend
  if key$=">" and chunk_idx<chunk_max then begin
    chunk_idx=chunk_idx+1
    cursor_x=0
  bend

  ' +/- = tempo change
  if key$="-" then tmpo%=tmpo%-1
  if key$="+" then tmpo%=tmpo%+1

  if key$="«" then key on:end
  if key$="{x85}" then gosub load
  if key$="{x86}" then gosub save

  ' F4 = save as text
  if key$="{x8A}" then gosub save_as_text

  ' return=play row
  if key$=chr$(13) then tempo tmpo% : play v$(cursor_y,chunk_idx)

  ' shift+return=play all channels
  if key$=chr$(141) then begin
    tempo tmpo%
    play v$(0,chunk_idx), v$(1,chunk_idx), {x5F}
    v$(2,chunk_idx), v$(3,chunk_idx), v$(4,chunk_idx), {x5F}
    v$(5,chunk_idx)
  bend

  gosub check_valid_key
  return


.runstop_trapper
'---------------
  if er=30 then resume  ' ignore run/stop
  trap
  print err$(er);" error"
  print " in lne";el
  end
ÿ